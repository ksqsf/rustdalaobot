{-# LANGUAGE OverloadedStrings, GeneralizedNewtypeDeriving #-}
module Main where

import Control.Monad
import Control.Monad.IO.Class
import Control.Concurrent
import Control.Applicative
import Data.Text (Text, isInfixOf)
import System.Random
import Telegram.Bot.API
import Telegram.Bot.Simple
import Telegram.Bot.Simple.Debug
import Telegram.Bot.Simple.UpdateParser

-- | guards made better-looking
must :: (Monad m, Alternative m) => m a -> (a -> Bool) -> m ()
must x f = x >>= guard . f

-- | Simple pattern matcher
type Pattern = Text -> Bool

lit = isInfixOf
neg = fmap not

infixl 3 .&.
(.&.) = liftA2 (&&)

infixl 2 .|.
(.|.) = liftA2 (||)

-- useful patterns
dalaoPattern, selfPattern, weakPattern, notPattern :: Pattern
dalaoPattern = patternFromWords dalaoWords .&. neg (patternFromWords weirdWords)
  where dalaoWords = ["å¤§ä½¬", "å¤§å“¥", "å¤ªä½¬", "å·¨ä½¬", "å¥†ä½¬"]
        weirdWords = ["å¤§å“¥å“¥"]
selfPattern = patternFromWords ["ä¿º", "æˆ‘", "å’±", "æœ¬", "äººå®¶", "ä¼¦å®¶", "ç§", "åœ¨ä¸‹", "å¦¾", "è€å­", "ä¸æ‰", "è‡ªå®¶", "é„™äºº", "æ•äºº", "è€å¨˜", "è€å¤«", "é˜®", "é˜¿æ‹‰", "äººå“‹", "å¶", "è—•", "å°å¼Ÿ", "å°å¦¹", "çˆ·", "ðŸ‘´", "å“¥", "å§", "å¾", "ä½™", "äºˆ", "å¬", "æ´’å®¶", "æŸ", "æœ•", "å­¤", "å“€å®¶", "è‡£", "ä¸‹å®˜", "å‘èŒ", "ä»†", "åƒ•", "å°å¼Ÿ", "æ„šå…„", "æ™šç”Ÿ", "è€æœ½", "è€åŸ", "è€èº«", "å§Ž", "è´«é“", "è´«åƒ§", "å°åƒ§", "è‰æ°‘", "å°å¯", "å°çš„"] .&. neg (lit "æœ¬å­" .|. lit "ç¬”è®°æœ¬" .|. lit "æœ¬æœ¬")
weakPattern = patternFromWords ["é¶¸", "èœ", "å¼±"]
notPattern = patternFromWords ["ä¸"]
questionPattern = patternFromWords ["?", "ï¼Ÿ", "ä½•", "ä¹ˆ", "å—", "å•¥", "å’‹", "å¸®"]
gratitudePattern = patternFromWords ["è°¢"]

patternFromWords :: [Text] -> Pattern
patternFromWords = foldl (.|.) (pure False) . map lit

-- | Rules based on incoming messages. The algebra of rules always chooses the first applicable one.
newtype Rule = MkRule { runRule :: Message -> Maybe Action }
  deriving (Semigroup, Monoid)

ruleFromPatBySender :: Text -> Pattern -> Action -> Message -> Maybe Action
ruleFromPatBySender username pat action msg = do
  (messageFrom msg >>= userUsername) `must` (== username)
  messageText msg `must` pat
  Just action

message :: Text
message = "ä¸å»ºè®®åœ¨äº¤æµä¸­ä½¿ç”¨â€œå¤§ä½¬â€â€œå¤§å“¥â€ç­‰ä¸å¿…è¦çš„ç§°è°“"

-- Rules for rust main group
ruleRustMain :: Rule
ruleRustMain = MkRule $ \msg -> do
  chatTitle (messageChat msg) `must` (== "Rust ä¼—")
  messageText msg `must` dalaoPattern
  Just (ReplyTo message (messageMessageId msg))

-- Rules for rust deep water group
ruleRustDeepWater :: Rule
ruleRustDeepWater = MkRule $ \msg -> do
  chatUsername (messageChat msg) `must` (== "rust_deep_water")
  noDalaoRule msg <> dcRule msg <> luoRule msg <> hjjRule msg
  where -- no dalao rule
        noDalaoRule msg = do
          messageText msg `must` (dalaoPattern .&. (questionPattern .|. gratitudePattern))
          Just (ReplyTo message (messageMessageId msg))
        -- DCè€å¸ˆ
        dcPattern = lit "å¥½æƒ³è®¤è¯†å¯çˆ±çš„åŒé©¬å°¾å°‘å¥³"
        dcRule = ruleFromPatBySender "DCjanus" dcPattern (ReplyDelay "#è’‚å¸è€æ¹¿çŠ¯ç—…è®¡æ•°å™¨")
        -- ç½—è€å¸ˆ
        luoPattern =     selfPattern .&. weakPattern .&. neg notPattern
                     .|. selfPattern .&. lit "å•¥éƒ½ä¸æ‡‚"
        luoRule = ruleFromPatBySender "driftluo" luoPattern (ReplyDelay "#ç½—è€å¸ˆå–å¼±è®¡æ•°å™¨")
        -- hjj
        hjjPattern = (selfPattern .|. lit "hjj") .&. lit "èººå¹³"
        hjjRule = ruleFromPatBySender "huangjj27" hjjPattern (ReplyDelay "#hjjåˆèººå¹³äº†")

-- Rules only for testing and debugging..
rulesDev :: Rule
rulesDev = MkRule $ \msg -> do
  chatTitle (messageChat msg) `must` (== "bot test group")
  messageText msg >>= Just . ReplyDelay

-- | Activated rules.
rules :: Rule
rules = ruleRustMain <> ruleRustDeepWater

-- | Bot conversation state model.
data Model = Model
  deriving (Show)

-- | Actions bot can perform.
data Action =
    NoAction
  | ReplyDelay Text -- ^ Simply send text with random delay
  | ReplyTo Text MessageId -- ^ Reply to a specific message.
  deriving (Show)

instance Semigroup Action where
  NoAction <> b = b
  a <> b = a

-- | Bot application.
bot :: BotApp Model Action
bot = BotApp
  { botInitialModel = Model
  , botAction = flip handleUpdate
  , botHandler = handleAction
  , botJobs = []
  }

-- | Process incoming 'Update's and turm them into 'Action's.
handleUpdate :: Model -> Update -> Maybe Action
handleUpdate _ update = do
  msg <- updateMessage update
  messageFrom msg `must` (not . userIsBot)
  runRule rules msg

-- | How to handle 'Action's.
handleAction :: Action -> Model -> Eff Action Model
handleAction NoAction model = pure model
handleAction (ReplyDelay message) model = model <# do
  liftIO $ do
    delay <- randomRIO (0, 10)   -- delay âˆˆ [0, 10 secs]
    threadDelay (delay * 1000000)
  replyText message
  return NoAction
handleAction (ReplyTo message id) model = model <# do
  reply $ ReplyMessage
    { replyMessageText = message
    , replyMessageParseMode = Nothing
    , replyMessageDisableWebPagePreview = Nothing
    , replyMessageDisableNotification = Just True
    , replyMessageReplyToMessageId = Just id
    , replyMessageReplyMarkup = Nothing
    }
  return NoAction

-- | Run bot with a given 'Token'.
run :: Token -> IO ()
run token = do
  env <- defaultTelegramClientEnv token
  startBot_ (traceBotDefault bot) env

main :: IO ()
main = getEnvToken "TELEGRAM_BOT_TOKEN" >>= run
